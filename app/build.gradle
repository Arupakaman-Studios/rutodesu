plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

ext {
    versionCodeGPlayAds = 1
    versionCodeGPlayPro = 1
    versionCodeFdroid = 1
}

android {

    signingConfigs {

        debug {
            storeFile file('../rutodesu_debug_android.keystore')
        }
        release {
            def props = new Properties()

            def fileInputStream = new FileInputStream(file('../secure.properties'))
            props.load(fileInputStream)
            fileInputStream.close()

            storeFile = file(props['storeFilePath'])
            storePassword = props['storePassword']
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.arupakaman.rutodesu"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode versionCodeGPlayAds
        versionName "1.0.0"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"

            manifestPlaceholders = [appLabel: "rutodesu Dev"]

            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "variant"
    productFlavors {
        adsGPlay {
            manifestPlaceholders = [appLabel: "rutodesu"]

            dimension "variant"
            buildConfigField "boolean", "isFDroid", "false"
            buildConfigField "boolean", "isAdsOn", "true"
        }
        proGPlay {
            manifestPlaceholders = [appLabel: "rutodesu pro"]

            applicationId "com.arupakaman.rutodesu.pro"
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "false"
            buildConfigField "boolean", "isAdsOn", "false"
        }
        fDroid {
            manifestPlaceholders = [appLabel: "rutodesu"]

            dimension "variant"
            buildConfigField "boolean", "isFDroid", "true"
            buildConfigField "boolean", "isAdsOn", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        useIR = true
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
    sourceSets {
        adsGPlay {
            res {
                srcDirs 'src/adsGPlay/res'
            }
            java {
                srcDirs 'src/adsGPlay/java'
            }
        }
        proGPlay {
            res {
                srcDirs 'src/proGPlay/res'
            }
            java {
                srcDirs 'src/proGPlay/java'
            }
        }
        fDroid {
            res {
                srcDirs 'src/fDroid/res'
            }
            java {
                srcDirs 'src/fDroid/java'
            }
        }
    }


}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.31"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-rc01'
    implementation "androidx.multidex:multidex:2.0.1"

    implementation project(':rootbeerlib')

    /**
     *   With Google Play Dependencies
     */
    adsGPlayImplementation platform('com.google.firebase:firebase-bom:26.5.0')
    adsGPlayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    adsGPlayImplementation "com.google.firebase:firebase-ads"
    adsGPlayImplementation 'com.google.android.play:core:1.10.0'
    adsGPlayImplementation "com.google.firebase:firebase-messaging"

    proGPlayImplementation platform('com.google.firebase:firebase-bom:26.5.0')
    proGPlayImplementation 'com.google.android.play:core:1.10.0'
    proGPlayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    proGPlayImplementation "com.google.firebase:firebase-messaging"

}